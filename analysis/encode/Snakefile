# reads sample info if provided
sample_info = {}
if 'sample_info' in config:
    inf = open(config['sample_info'], 'rt')
    heads = []
    for ii, line in enumerate(inf):
        line = line.rstrip().split('\t')
        if ii == 0:
            heads = line
        else:
            sinfo = {}
            for coli, headn in enumerate(heads):
                sinfo[headn] = line[coli]
            sample_info[sinfo['sample']] = sinfo

# if no sample(s) specified, analyze all the ones in the info TSV
if 'sample' not in config:
    config['sample'] = list(sample_info.keys())

#
#
# ## main rules
#
#

rule main:
    input:
        cov=expand('results/{sample}/{sample}.cov.gaf.bgz.tbi', sample=config['sample']),
        readstat=expand('results/{sample}/{sample}.gaf.wc.txt', sample=config['sample']),
        sum=expand('results/{sample}/{sample}.cov.sum.tsv', sample=config['sample'])

#
#
# ## tasks
#
#

rule dwl_reads:
    output: temp('reads/{sample}.{part}.fastq.gz')
    params:
        url=lambda wildcards: sample_info[wildcards.sample]['url_fq' + wildcards.part]
    localrule: True
    shell: "wget --quiet -O {output} {params.url}"

rule dwl_fastq_shuffle:
    output: 'fastq-shuffle.pl'
    localrule: True
    shell: "wget --quiet -O {output} https://raw.githubusercontent.com/chloroExtractorTeam/fastq-shuffle/master/fastq-shuffle.pl"

rule shuffle_reads:
    input:
        script='fastq-shuffle.pl',
        fq1='reads/{sample}.1.fastq.gz',
        fq2='reads/{sample}.2.fastq.gz'
    output:
        fq1=temp('reads_shuffled/{sample}.1.fastq.gz'),
        fq2=temp('reads_shuffled/{sample}.2.fastq.gz')
    params: 
        fq1='reads/{sample}.1.fastq',
        fq2='reads/{sample}.2.fastq'
    resources:
        mem='16G',
        runtime='12h'
    shell:
        """
        zcat {input.fq1} > {params.fq1}
        zcat {input.fq2} > {params.fq2}
        perl {input.script} -s 5G -1 {params.fq1} -2 {params.fq2}
        rm {params.fq1} {params.fq2}
        gzip {params.fq1}.shuffled {params.fq2}.shuffled
        mv {params.fq1}.shuffled.gz {output.fq1}
        mv {params.fq2}.shuffled.gz {output.fq2}
        """

# map reads and call peaks
rule map_reads:
    input:
        gbz=config['gbz'],
        dist=config['dist'],
        min=config['min'],
        fq1='reads_shuffled/{sample}.1.fastq.gz',
        fq2='reads_shuffled/{sample}.2.fastq.gz'
    output: 'results/{sample}/{sample}.gaf.gz'
    log: 'log/map_reads.{sample}.log'
    benchmark: 'benchmark/map_reads.{sample}.tsv'
    container: 'docker://quay.io/jmonlong/vg-work:1.59.0_v1'
    resources:
        mem='100G',
        runtime='24h'
    threads: 8
    shell:
        """
        vg giraffe -b fast -p -t {threads} -Z {input.gbz} -d {input.dist} -m {input.min} -f {input.fq1} -f {input.fq2} -o gaf | gzip > {output}
        """

rule convert_gbz_to_pg:
    input: config['gbz']
    output: 'results/pg/pg.pg'
    threads: 2
    benchmark: 'benchmark/convert_gbz_to_pg.tsv'
    container: 'docker://quay.io/jmonlong/vg-work:1.59.0_v1'
    resources:
        mem='32G',
        runtime='3h'
    shell:
        """
        vg convert -H -t {threads} {input} > {output}
        """

rule count_reads:
    input: 'results/{sample}/{sample}.gaf.gz',
    output: 'results/{sample}/{sample}.gaf.wc.txt'
    threads: 1
    resources:
        mem='2G',
        runtime='3h'
    shell: "zcat {input} | wc -l > {output}"

rule make_cov:
    input:
        gbz=config['gbz'],
        pg='results/pg/pg.pg',
        reads='results/{sample}/{sample}.gaf.gz',
        script='make_coverage_track.py'
    output: temp('results/{sample}/{sample}.cov.gaf.gz')
    threads: 4
    resources:
        mem='120G',
        runtime='24h'
    benchmark: 'benchmark/make_cov.{sample}.tsv'
    container: 'docker://quay.io/jmonlong/vg-work:1.59.0_v1'
    shell:
        """
        vg pack -Q 1 -t {threads} -x {input.gbz} -a {input.reads} -d | python3 {input.script} -g {input.pg} -b 1,5,30 | gzip > {output}
        """

rule sort_cov_gaf:
    input: 'results/{sample}/{sample}.cov.gaf.gz'
    output: 'results/{sample}/{sample}.cov.gaf.bgz'
    benchmark: 'benchmark/sort_cov_gaf.{sample}.tsv'
    container: 'docker://quay.io/jmonlong/vg-work:1.59.0_v1'
    threads: 2
    resources:
        mem='8G',
        runtime='3h'
    shell: "vg gamsort -G {input} | bgzip > {output}"

rule index_gaf:
    input: 'results/{sample}/{sample}.cov.gaf.bgz'
    output: 'results/{sample}/{sample}.cov.gaf.bgz.tbi'
    container: 'docker://quay.io/jmonlong/vg-work:1.59.0_v1'
    benchmark: 'benchmark/index_gaf.{sample}.tsv'
    localrule: True
    shell: "tabix -p gaf {input}"
    
rule summarize_cov_gaf:
    input:
        cov='results/{sample}/{sample}.cov.gaf.bgz',
        script='summarize_cov_gaf.py'
    output: 'results/{sample}/{sample}.cov.sum.tsv'
    resources:
        mem='4G',
        runtime='3h'
    shell: "zcat {input.cov} | python3 {input.script} > {output}"
